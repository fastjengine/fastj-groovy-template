/*
 * FastJ Groovy Template Build Script
 *
 * Thank you for choosing FastJ! This is a Gradle-based build script that will help you manage your
 * project with ease. */

plugins {
    /* To begin with, Gradle needs the 'groovy' plugin so that it knows this is a Groovy project. */
    id 'groovy'
    /* This template is for an application -- we'll need this plugin to make sure Gradle knows
     * this, too. */
    id 'application'
    /* To create distributable files for your game, we use this runtime plugin. */
    id 'org.beryx.runtime' version '1.12.5'
}

/* Your project's group name goes here.
 * This should be a domain name you own.
 * If you don't own a domain, don't worry! You can always set this to 'io.github.yourgithubusername'. */
group('io.github.yourgithubusername')

/* Your project's version.
 * When you want to distribute a new version of your project, always make sure to update the
 * project version. */
version('0.0.1')

/* Your project's description.
 * Give a one or two sentence description of your project -- if you publish it somewhere, you'll be
 * able to use it. */
description('A Groovy Template for FastJ.')


/* When you add a dependency on another project (like FastJ), you need to add specify where the
 * dependencies are coming from!
 * FastJ is hosted on Maven Central and Jitpack.io, so we"ll add the jitpack.io dependency here. */
repositories.maven {
    url('https://jitpack.io/')
}
repositories.mavenCentral()

/* The dependency for FastJ, the game engine this template depends on. */
dependencies.implementation('com.github.fastjengine:FastJ:1.5.0')
/* The dependency for the Groovy programming language. */
dependencies.implementation('org.codehaus.groovy:groovy:3.0.4')

/* Here, we specify where the main entrypoint of the project.
 * Feel free to change this as needed. */
application.getMainClass().set('tech.fastj.template.Game')


/* The Runtime plugin is used to configure the executables and other distributions for your
 * project. */
runtime {

    options.addAll(
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages'
    )

    launcher {
        applicationName = project.name
        noConsole = true
    }

    jpackage {
        /* Use this to define the path of the icons for your project. */
        def iconPath = 'project-resources/fastj_icon'
        def currentOs = org.gradle.internal.os.OperatingSystem.current()

        installerOptions += [
                '--description', project.description,
                '--vendor', project.group,
                '--app-version', project.version
        ]


        if (currentOs.windows) {
            installerType = 'msi'
            imageOptions += ['--icon', "${iconPath}.ico"]
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-shortcut']
        } else if (currentOs.linux) {
            installerType = 'deb'
            imageOptions += ['--icon', "${iconPath}.png"]
            installerOptions += ['--linux-shortcut']
        } else if (currentOs.macOsX) {
            installerType = 'pkg'
            imageOptions += ['--icon', "${iconPath}.icns"]
            installerOptions += ['--mac-package-name', project.name]
        }
    }
}

